buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.yaml:snakeyaml:2.0'
    }
}


import org.yaml.snakeyaml.DumperOptions
import org.yaml.snakeyaml.Yaml

tasks.register('updatePromptTemplates') {
    group = 'build'
    description = 'Update application-prompt_template.yml with prompt files from resources/prompts'

    doLast {
        def promptsDir = file('src/main/resources/prompts')
        def yamlFile = file('src/main/resources/application-prompt_template.yml')

        if (!promptsDir.exists()) {
            println "Prompts directory does not exist"
            return
        }

        if (yamlFile.exists()) {
            yamlFile.delete()
        }

        yamlFile.createNewFile()
        def data = new LinkedHashMap()
        data.put('ai', new LinkedHashMap())
        data['ai'].put('prompt', new LinkedHashMap())
        data['ai']['prompt'].put('templates', new LinkedHashMap())

        def templates = data['ai']['prompt']['templates']
        promptsDir.eachDir { dir ->
            def systemFile = new File(dir, 'system.md')
            def userFile = new File(dir, 'user.md')

            if (systemFile.exists() && systemFile.text) {
                if (!templates.containsKey(dir.name)) {
                    templates.put(dir.name, new LinkedHashMap())
                }
                templates[dir.name].put('system', systemFile.text)
            }

            if (userFile.exists() && userFile.text) {
                if (!templates.containsKey(dir.name)) {
                    templates.put(dir.name, new LinkedHashMap())
                }
                templates[dir.name].put('user', userFile.text)
            }
        }

        def options = new DumperOptions()
        options.setDefaultFlowStyle(DumperOptions.FlowStyle.AUTO)
        options.setPrettyFlow(true)
        options.setIndent(2)
        options.setIndicatorIndent(0)
        options.setWidth(80)
        def yamlWithFormatting = new Yaml(options)

        def writer = new StringWriter()
        yamlWithFormatting.dump(data, writer)
        yamlFile.write(writer.toString())
    }
}

tasks.named('compileJava') {
    dependsOn updatePromptTemplates
}

jar {
    enabled = true
    archiveClassifier = ''
    version = project.version
}

bootJar {
    enabled = false
}

dependencies {
    implementation project(':common')
    implementation project(':auth-audit')
    implementation 'org.springframework.ai:spring-ai-starter-model-anthropic'
    implementation 'org.springframework.ai:spring-ai-starter-model-ollama'
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'
    implementation "com.baomidou:mybatis-plus-boot-starter:$rootProject.mybatisPlusVer"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$rootProject.springDocVer"
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    implementation 'org.springframework.ai:spring-ai-starter-model-chat-memory-repository-jdbc'
    implementation "com.github.vertical-blank:sql-formatter:$rootProject.sqlFormatterVer"
    implementation "com.alibaba:transmittable-thread-local:${rootProject.ttlVer}"
    implementation "org.redisson:redisson-spring-boot-starter:$rootProject.redissonVer"
    implementation 'org.springframework.security:spring-security-core'
    implementation "com.baomidou:dynamic-datasource-spring-boot3-starter:${rootProject.dynamicDsVer}"
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc'
    runtimeOnly 'com.oracle.database.jdbc:ojdbc11'
}