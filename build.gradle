plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'

    // frontend
    id 'org.siouan.frontend-jdk21' version '10.0.0' apply false

    // code style
    id 'com.diffplug.spotless' version '7.0.2'
}

jar.enabled = false
bootJar.enabled = false

ext {
    // dependencies version
    commonsLang3Ver = '3.20.0'
    springCloudVer = '2025.0.1'
    mybatisVer = '3.0.5'
    zxingVer = '3.5.4'
    commonsCodecVer = '1.20.0'
    vavrVer = '0.11.0'
    mybatisPlusVer = '3.5.15'
    commonsPool2Ver = '2.13.0'
    springDocVer = '2.8.14'
    commonsCollections4Ver = '4.5.0'
    browscapJavaVer = '1.5.1'
    commonsIoVer = '2.19.0'
    joseJwtVer = '10.5'
    ttlVer = '2.14.5'
    dynamicDsVer = '4.5.0'
    lokiVer = "1.6.0-m1"
    httpClient4Ver = '4.5.14'
    ajCaptchaVer = '1.3.0'
    ip2RegionVer = '2.7.0'
    redissonVer = '3.52.0'
    javersVer = '7.8.0'
    uuidCreatorVer = '6.1.1'
    jexl3Ver = '3.5.0'
    springAiVer = '1.1.2'
    sqlFormatterVer = '2.0.5'
    snakeYamlVer = '2.5'
    easyexcelVer = '4.0.3'
    poiVer = '5.3.0'
    webauthn4jVer = '0.31.0.RELEASE'
    awsS3Ver = '2.41.34'
}

allprojects {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/central/' }
        maven { url 'https://maven.aliyun.com/repository/public/'}
        mavenCentral()
        def repoPassword = System.getenv("REPO_OPENSRCDEVELOP_PASSWORD")
        if (repoPassword != null && !repoPassword.isEmpty()) {
            maven {
                url = 'https://packages.aliyun.com/6512e2d3db8fc6072ca0c88c/maven/repo-opensrcdevelop'
                credentials {
                    username = '6512e2bc3f15d5487d5296cb'
                    password = repoPassword
                }
            }
        }
    }

    if (it.name != 'ui') {
        apply plugin: 'java'
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'

        group = 'cn.opensrcdevelop'

        java {
            sourceCompatibility = '21'
        }

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
        }

        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter'
            implementation "org.apache.commons:commons-lang3:$rootProject.commonsLang3Ver"
            implementation "commons-codec:commons-codec:$rootProject.commonsCodecVer"
            implementation "org.apache.commons:commons-collections4:$rootProject.commonsCollections4Ver"
            implementation "commons-io:commons-io:$rootProject.commonsIoVer"
            implementation "io.vavr:vavr:$rootProject.vavrVer"
            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
            annotationProcessor 'org.projectlombok:lombok'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            testImplementation 'org.assertj:assertj-core'
            testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
            testAnnotationProcessor 'org.projectlombok:lombok'
            testCompileOnly 'org.projectlombok:lombok'
        }

        dependencyManagement {
            imports {
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:$rootProject.springCloudVer"
                mavenBom "org.springframework.ai:spring-ai-bom:${springAiVer}"
            }
        }

        tasks.named('test') {
            useJUnitPlatform()
        }

        // Apply spotless to all Java modules except ui
        if (it.name != 'ui') {
            apply plugin: 'com.diffplug.spotless'

            spotless {
                java {
                    // Use Eclipse JDT formatter with IDEA-style settings
                    eclipse().configFile(rootProject.file('config/spotless/eclipse-java-style.xml'))
                    // Disable some import-related rules
                    importOrder()
                    removeUnusedImports()
                    trimTrailingWhitespace()
                    endWithNewline()

                    // Target files
                    target '**/*.java'

                    // Exclude generated files
                    targetExclude '**/generated/**', '**/generated-sources/**', '**/build/**'
                }
            }
        }
    }
}
