plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'

    // frontend
    id 'org.siouan.frontend-jdk21' version '10.0.0' apply false
}

jar.enabled = false
bootJar.enabled = false

ext {
    // dependencies version
    commonsLang3Ver = '3.14.0'
    springCloudVer = '2025.0.0'
    mybatisVer = '3.0.4'
    zxingVer = '3.5.3'
    commonsCodecVer = '1.18.0'
    vavrVer = '0.10.6'
    mybatisPlusVer = '3.5.12'
    commonsPool2Ver = '2.12.1'
    springDocVer = '2.8.6'
    commonsCollections4Ver = '4.5.0-M1'
    browscapJavaVer = '1.5.1'
    commonsIoVer = '2.19.0'
    joseJwtVer = '10.3'
    ttlVer = '2.14.5'
    dynamicDsVer = '4.3.1'
    lokiVer = "1.6.0-m1"
    httpClient4Ver = '4.5.14'
    ajCaptchaVer = '1.3.0'
    ip2RegionVer = '2.7.0'
    redissonVer = '3.50.0'
    javersVer = '7.8.0'
    uuidCreatorVer = '6.1.1'
}

allprojects {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/central/' }
        maven { url 'https://maven.aliyun.com/repository/public/'}
        mavenCentral()
        maven {
            url = 'https://packages.aliyun.com/6512e2d3db8fc6072ca0c88c/maven/repo-opensrcdevelop'
            credentials {
                username = '6512e2bc3f15d5487d5296cb'
                password = System.getenv("REPO_OPENSRCDEVELOP_PASSWORD")
            }
        }
    }

    if (it.name != 'ui') {
        apply plugin: 'java'
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'

        group = 'cn.opensrcdevelop'

        java {
            sourceCompatibility = '21'
        }

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
        }

        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter'
            implementation "org.apache.commons:commons-lang3:$rootProject.commonsLang3Ver"
            implementation "commons-codec:commons-codec:$rootProject.commonsCodecVer"
            implementation "org.apache.commons:commons-collections4:$rootProject.commonsCollections4Ver"
            implementation "commons-io:commons-io:$rootProject.commonsIoVer"
            implementation "io.vavr:vavr:$rootProject.vavrVer"
            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
            annotationProcessor 'org.projectlombok:lombok'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            testImplementation 'org.assertj:assertj-core'
            testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
            testAnnotationProcessor 'org.projectlombok:lombok'
            testCompileOnly 'org.projectlombok:lombok'
        }

        dependencyManagement {
            imports {
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:$rootProject.springCloudVer"
            }
        }

        tasks.named('test') {
            useJUnitPlatform()
        }
    }
}
